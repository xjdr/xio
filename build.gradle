plugins {
  id 'java'
  id 'net.researchgate.release' version '2.7.0'
  id 'com.jfrog.bintray' version '1.8.0'
  id 'com.github.sherter.google-java-format' version '0.6'
  id 'jacoco'
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'com.github.sherter.google-java-format'

  repositories {
    jcenter()
  }

  release {
    tagTemplate = 'v${version}'
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    // todo: (WK) - make these java-library plugin "api" dependencies when we get the release plugin to play nice
    implementation group: 'com.google.guava', name: 'guava', version: guava_version
    implementation group: 'com.typesafe', name: 'config', version: typesafe_config_version
    implementation group: 'io.netty', name: 'netty-all', version: netty_version
    implementation group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: netty_tcnative_version
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: slf4j_version
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: auto_value_version
    implementation group: 'com.google.auto.value', name: 'auto-value', version: auto_value_version
    annotationProcessor "org.projectlombok:lombok:$lombok_version"
    implementation group: 'org.projectlombok', name: 'lombok', version: lombok_version
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.60'

    testImplementation project(':xio-test')
    testImplementation group: 'junit', name: 'junit', version: junit_version
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: hamcrest_version
  }

  jacocoTestReport {
    reports {
      xml.enabled = true // coveralls plugin depends on xml format report
      html.enabled = true
      html.destination file("${buildDir}/jacocoHtml")
    }
  }

}

googleJavaFormat {
  exclude "*/out/**/*"
  exclude "*/target/generated-sources/**/*.java"
  exclude "*/target/generated-test-sources/**/*.java"
  exclude "/target/**/*.java"
}

task('print-alpn') {
  apply from: 'alpn-boot.gradle'
  doLast {
    println xbcp
  }
}

task verifyBintrayConfig {
  doLast {
    println "bintray_user: $bintray_user"
    println "bintray_apikey: $bintray_apikey"
    println "bintray_dryrun: ${bintray_dryrun}"
  }
}

afterReleaseBuild.dependsOn ':bintrayUpload'

task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
}
