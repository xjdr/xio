plugins {
  id 'java-library'
  id 'com.google.protobuf' version '0.8.5'
  id 'com.github.kt3k.coveralls' version '2.8.2'
  id 'jacoco'
  id 'com.jfrog.bintray'
}

description = 'xio'
group = project_group

apply from: '../alpn-boot.gradle'

dependencies {
  api project(':xio-tls')
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jackson_core_version
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_databind_version
  implementation group: 'com.google.code.findbugs', name: 'jsr305', version: findbugs_jsr305_version
  implementation(group: 'org.apache.curator', name: 'curator-framework', version: curator_version) {
    exclude(module: 'zookeeper')
  }
  implementation(group: 'org.apache.curator', name: 'curator-recipes', version: curator_version) {
    exclude(module: 'zookeeper')
  }
  implementation(group: 'org.apache.zookeeper', name: 'zookeeper', version: zookeeper_version) {
    exclude(module: 'slf4j-log4j12')
    exclude(module: 'log4j')
  }
  implementation group: 'org.apache.thrift', name: 'libthrift', version: libthrift_version
  implementation group: 'io.opentracing', name: 'opentracing-api', version: '0.31.0'
  implementation group: 'io.opentracing', name: 'opentracing-util', version: '0.31.0'
  implementation group: 'io.opentracing.brave', name: 'brave-opentracing', version: '0.32.0'
  implementation group: 'com.datadoghq', name: 'dd-trace-api', version: '0.14.0'
  implementation group: 'com.datadoghq', name: 'dd-trace-ot', version: '0.14.0'
  implementation group: 'io.zipkin.brave', name: 'brave-instrumentation-http', version: zipkin_brave_brave_instrumentation_http_version
  implementation group: 'io.zipkin.brave', name: 'brave-context-slf4j', version: zipkin_brave_brave_context_slf4j_version
  implementation group: 'io.zipkin.reporter2', name: 'zipkin-sender-okhttp3', version: zipkin_reporter_zipkin_sender_okhttp3_version

  implementation group: 'io.dropwizard.metrics', name: 'metrics-core', version: metrics_version
  implementation group: 'io.dropwizard.metrics', name: 'metrics-jmx', version: metrics_version

  implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpc_version

  //region tests
  testImplementation group: 'net.jodah', name: 'concurrentunit', version: concurrentunit_version
  testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logback_version
  testImplementation group: 'ch.qos.logback', name: 'logback-core', version: logback_version
  testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: groovy_version
  testImplementation(group: 'org.apache.curator', name: 'curator-test', version: curator_test_version) {
    exclude(module: 'zookeeper')
  }
  testImplementation group: 'io.zipkin.brave', name: 'brave-instrumentation-http-tests', version: zipkin_brave_brave_instrumentation_http_version
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: eclipse_jetty_server_version
  testImplementation(group: 'org.mockito', name: 'mockito-core', version: mockito_version)
  testImplementation(group: 'org.powermock', name: 'powermock-core', version: powermock_version)
  testImplementation(group: 'org.powermock', name: 'powermock-module-junit4', version: powermock_version)
  testImplementation(group: 'org.powermock', name: 'powermock-api-mockito2', version: powermock_version)
  testImplementation group: 'org.eclipse.jdt.core.compiler', name: 'ecj', version: eclipse_jdt_core_compiler_ecj_version
  testImplementation group: 'io.grpc', name: 'grpc-core', version: grpc_version
  testImplementation(group: 'io.grpc', name: 'grpc-netty', version: grpc_version) {
    exclude(module: 'grpc-core')
    exclude(module: 'netty-codec-http2')
  }
  testImplementation group: 'io.grpc', name: 'grpc-stub', version: grpc_version
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.5.1-1'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

sourceSets {
  test {
    java {
      srcDirs += 'build/generated/source/proto/test/grpc'
      srcDirs += 'build/generated/source/proto/test/java'
    }
    proto {
      // In addition to the default 'src/test/proto'
      srcDir 'src/test/proto'
    }
  }
}

bintray_package = 'xio'
publishing {
  publications {
    maven(MavenPublication) {
      groupId project_group
      artifactId bintray_package
      version version
      from components.java
    }
  }
}

bintray {
  user = bintray_user //this usually comes from gradle.properties file in ~/.gradle
  key = bintray_apikey //this usually comes from gradle.properties file in ~/.gradle
  publications = ['maven'] //this refers to publishing.publications.maven up above ^^^
  dryRun = bintray_dryrun.toBoolean() //Whether to run this as dry-run, without deploying
  publish = true //If version should be auto published after an upload
  pkg {
    userOrg = bintray_subject
    repo = bintray_repo
    name = bintray_package
    version {
      name = project.version //Bintray logical version name
    }
    licenses = ['Apache-2.0']
    websiteUrl = 'https://github.com/xjdr/xio'
    issueTrackerUrl = 'https://github.com/xjdr/xio/issues'
    vcsUrl = 'https://github.com/xjdr/xio.git'
  }
}
